package security;

import dao.history.HistoricalDao;
import dao.UserDao;
import manager.configurations.EpasParam;
import models.enumerate.CodesForEmployee;
import models.enumerate.StampTypes;
import models.enumerate.AccountRole;
import models.enumerate.AttachmentType;
import models.Absence;
import models.AbsenceType;
import models.Attachment;
import models.Configuration;
import models.Notification;
import models.Office;
import models.Person;
import models.PersonConfiguration;
import models.PersonDay;
import models.Qualification;
import models.Role;
import models.ShiftType;
import models.Stamping;
import models.UsersRolesOffices;
import security.PermissionCheck;

global models.User currentOperator;


/* Metodi senza regole
*
*  Administration.createOvertimeFile
*  JsonExport.activePersons
*
*
*
*/

/*
 * Gli utenti disabilitati non possono far nulla
 */
rule disabledUser
when
  eval (currentOperator.disabled)
  $c: PermissionCheck()
then
   $c.revoke();
end

rule AnyUser
when
  $c: PermissionCheck(action in (
     	"Application.index",
     	"Version.showVersion",
     	"Notifications.list",
     	"Notifications.archiveds",
     	"Notifications.readAndRedirect",
     	"Notifications.read"
       ), target == null, granted == false)
then
 $c.grant();
end

/*
 *	Azioni eseguibili dagli impiegati per visualizzare la propria situazione.
 *  (Ricalca il più possibile la struttura del dropdown menu della persona
 *  oltre i calendari iCal e le azioni REST)
 */

rule isEmployeeAbsencesVisible
when
  $uro: UsersRolesOffices()
  Role(name == Role.EMPLOYEE) from $uro.role
  $p: Person() from currentOperator.person
  $o: Office() from $p.office
  Configuration(epasParam == EpasParam.ABSENCES_FOR_EMPLOYEE, fieldValue == true) from $o.configurations
  $c: PermissionCheck(action in (
  		"Absences.absencesVisibleForEmployee"), granted == false, target == null)
then
 $c.grant();
end


rule IsEmployee
when
  $uro: UsersRolesOffices()
  Role(name == Role.EMPLOYEE) from $uro.role

  $c: PermissionCheck(action in (
  		/* Ritorna allo user developer */
     	"Administration.restoreUser",

     	/* Situazione Mensile */
     	"Stampings.stampings",

     	/* Assenze mensili */
     	"Absences.absences",
     	"Absences.absenceInMonth",

     	/* Assenze annuali */
     	"Absences.absencesPerPerson",

     	/* Ferie */
     	"Vacations.show",
     	"Vacations.permissionCurrentYear",
     	"Vacations.vacationsCurrentYear",
       	"Vacations.vacationsLastYear",

       	/* Competenze */
     	"Competences.competences",

       	/* Riepilogo orario*/
     	"PersonMonths.hourRecap",

     	/*Riepilogo buoni pasto */
     	"MealTickets.mealTickets",

     	/* Ore di formazione */
     	"PersonMonths.trainingHours",
     	"PersonMonths.deleteTrainingHours",
     	"PersonMonths.deleteTrainingHoursConfirmed",
     	"PersonMonths.insertTrainingHours",

     	"PersonMonths.insertTrainingHoursPreviousMonth",
     	"PersonMonths.modifyTrainingHours",
     	"PersonMonths.saveTrainingHours",
     	"PersonMonths.updateTrainingHours",

     	/* Modifica password */
     	"Persons.changePassword",
     	"Persons.savePassword",
     	"Persons.resetPassword",

     	/* Stampa cartellino */
     	"PrintTags.showTag",

     	/*
     	 * Visualizzazione eventuali turni via iCal
     	 * dentro il metodo controllare il permesso di
     	 * visualizzare il calendario specifico richiesto
     	 */
     	"Shift.iCal",
     	"Reperibility.iCal",

     	/* Metodi rest */
     	"rest.PersonDays.getDaySituation",
     	"rest.PersonDays.getMonthSituation",

     	/* Timbrature web */
     	"Clocks.daySituation",
     	"Clocks.webStamping",
     	"Clocks.insertWebStamping",

     	/*Possibilità di inviare report*/
     	"ReportCentre.generateReport",
     	"ReportCentre.sendProblem"


       ), target == null, granted == false)
then
 $c.grant();
end

rule personInCharge
when
  UsersRolesOffices(role.name == Role.EMPLOYEE)
  Person (isPersonInCharge == true) from currentOperator.person
  $c: PermissionCheck( action in ("Stampings.dailyPresenceForPersonInCharge" ,
    "Competences.monthlyOvertime" ), granted == false)
then
  $c.grant();
end

/*
 * L'amministratore del personale può modificare una determinata assenza se:
 */
rule AdminCanEditAbsence
when
 $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN)
 $a: Absence()
 $pd: PersonDay() from $a.personDay
 Office(persons.contains($pd.person)) from $uro.office
 $c: PermissionCheck(target == $a, granted == false )
then
 $c.grant();
end

/*
 * L'amministratore del personale può modificare una determinata timbratura se:
 */
rule AdminCanEditStamping
when
 $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN)
 $target: Stamping()
 Office(persons.contains($target.getOwner())) from $uro.office
 $c: PermissionCheck(action in ("Stampings.edit", "Stampings.save"), target == $target, granted == false )
then
 $c.grant();
end

/* Azioni eseguibili da qualsiasi utente che abbiano come destinatario
 * dell'azione se stesso
 */
rule HasRightOnHimself
when
  $p: Person(this == currentOperator.person)
  $c: PermissionCheck(action not in (
      "Stampings.blank",
      "Absences.blank",
      "Absences.edit",
      "Absences.delete",
      "Absences.save"
  ),target == $p, granted == false)
then
 $c.grant();
end

/*
 * L'Amministratore del personale può fare tutto sulle persone del proprio ufficio
 */
rule PersonnelAdmin_onPerson
when
  $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN)
  $o: Office() from $uro.office
  $c: PermissionCheck($o.persons contains target, granted == false)
then
 $c.grant();
end

/*
 * L'Amministratore del personale in sola lettura può fare quasi tutto sulle persone del proprio ufficio
 */
rule HasRightOnPerson
when
  $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN_MINI)
  $o: Office() from $uro.office
  $c: PermissionCheck(action not in (
    "Stampings.blank",
    "Absences.blank",
    "Absences.edit",
    "Absences.delete",
    "Absences.save"), $o.persons contains target, granted == false)
then
 $c.grant();
end


/*******************************************************************************
 * Azioni ruolo DEVELOPER e ADMIN
 ******************************************************************************/
rule systemUsers
salience 1
activation-group 'admin'
when
 AccountRole(this in (AccountRole.DEVELOPER,AccountRole.ADMIN))
 $c: PermissionCheck( granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Azioni non permesse per il ruolo ADMIN
 ******************************************************************************/
rule revokeToAdmin
salience 2
activation-group 'admin'
when
 AccountRole(this == AccountRole.ADMIN)
 $c: PermissionCheck(
    action.startsWith("Absences.")
    || action.startsWith("Administration.")
    || action.startsWith("Charts.")
    || action.startsWith("MonthRecaps.")
    || action.startsWith("Competences.")
    || action.startsWith("MealTickets.")
    || action.startsWith("PrintTags.")
    || action.startsWith("PersonMonths.")
    || action.startsWith("Stampings.")
    || action.startsWith("UploadSituation.")
    || action.startsWith("Vacations.")
    || action.startsWith("VacationsAdmin."))
then
 $c.revoke();
end

/*******************************************************************************
 * Azioni ruolo RO_ADMIN (Admin in sola lettura)
 ******************************************************************************/
rule ro_admin
when
  AccountRole(this == AccountRole.RO_ADMIN)
  $c: PermissionCheck(action in (
    "Absences.absenceInPeriod",
    "Absences.manageAbsenceCode",
    "Absences.manageAttachmentsPerCode",
    "Absences.manageAttachmentsPerPerson",
    "Absences.showGeneralMonthlyAbsences",
    "Absences.showPersonMonthlyAbsences",
    "Absences.yearlyAbsences",
    "BadgeReaders.list",
    "BadgeSystems.list",
    "BadgeSystems.personBadges",
    "Certifications.certifications",
    "Charts.checkLastYearAbsences",
    "Charts.overtimeOnPositiveResidual",
    "Charts.overtimeOnPositiveResidualInYear",
    "Competences.activateServices",
    "Competences.approvedCompetenceInYear",
    "Competences.enabledCompetences",
    "Competences.manageCompetenceCode",
    "Competences.showCompetences",
    "Competences.totalOvertimeHours",
    "Configurations.personShow",
    "Configurations.show",
    "Contracts.edit",
    "Contracts.personContracts",
    "Contracts.updateContractVacationPeriod",
    "Contracts.updateContractWorkingTimeType",
    "Contracts.updateSourceContract",
    "Institutes.list",
    "MealTickets.editPersonMealTickets",
    "MealTickets.findCodeBlock",
    "MealTickets.personMealTickets",
    "MealTickets.recapMealTickets",
    "MealTickets.recapPersonMealTickets",
    "MealTickets.returnedMealTickets",
    "MonthRecaps.customRecap",
    "MonthRecaps.showRecaps",
    "PersonDayHistory.personDayHistory",
    "PersonMonths.visualizePeopleTrainingHours",
    "Persons.children",
    "Persons.edit",
    "Persons.list",
    "Persons.showCurrentContractWorkingTimeType",
    "Persons.showCurrentVacation",
    "PrintTags.listPersonForPrintTags",
    "PrintTags.showTag",
    "Stampings.dailyPresence",
    "Stampings.missingStamping",
    "Stampings.personStamping",
    "VacationsAdmin.list",
    "VacationsAdmin.permissionCurrentYear",
    "VacationsAdmin.vacationsCurrentYear",
    "VacationsAdmin.vacationsLastYear",
    "WorkingTimes.manageOfficeWorkingTime",
    "WorkingTimes.manageWorkingTime",
    "WorkingTimes.showContract",
    "WorkingTimes.showContractWorkingTimeType",
    "WorkingTimes.showHorizontal",
    "WorkingTimes.showWorkingTimeType"),  granted == false)
then
  $c.grant();
end

/*******************************************************************************
 * Azioni utente con ruolo TECHNICAL_ADMIN
 ******************************************************************************/
rule TechnicalAdmin_Generic
when
 $uro: UsersRolesOffices()
 Role(name == Role.TECHNICAL_ADMIN) from $uro.role
 $c: PermissionCheck(action in (

        "Users.edit",
        "Users.show",
        "Users.addRole",
        "Users.saveRole",
        "Users.removeRole",

        "Institutes.list",
        "Institutes.index",

	    "BadgeReaders.index",
	    "BadgeReaders.list",
	    "BadgeReaders.blank",

	    "BadgeSystems.index",
	    "BadgeSystems.list",
	    "BadgeSystems.blank",

        /* duplicate prima di avere il contesto office */
		"Institutes.edit",
		"Institutes.save",
		"Institutes.delete",

		"Offices.list",
		"Offices.edit",
		"Offices.save",

		"BadgeReaders.save",
		"BadgeReaders.edit",
	    "BadgeReaders.updateInfo",
	    "BadgeReaders.changePassword",
		"BadgeReaders.joinBadgeSystems",
		"BadgeReaders.saveBadgeSystems",
		"BadgeReaders.delete",

		"BadgeSystems.save",
		"BadgeSystems.edit",
		"BadgeSystems.updateInfo",
		"BadgeSystems.joinBadges",
	    "BadgeSystems.saveBadges",
	    "BadgeSystems.deleteBadge",
	    "BadgeSystems.joinPersonNumbers",
  		"BadgeSystems.joinOldBadgeNumbers",
  		"BadgeSystems.delete",
  		"Users.list",
  		"Users.systemList",
  		"Users.blank",
  		"Users.edit",
  		"Users.updateInfo",
  		"Users.updateRole",
  		"Users.index",
  		"Users.systemBlank",
  		"Users.save",
  		"Users.disable",
  		"Users.disabledList",
  		"Users.enable",

		"Administrators.blank",
		"Administrators.save",
		"Administrators.delete"

        ), target == null, granted == false)
then
 $c.grant();
end

rule TechnicalAdmin_targetUser
when
 $uro: UsersRolesOffices(role.name == Role.TECHNICAL_ADMIN)
 $u: User(this.hasRelationWith($uro.office))
 $c: PermissionCheck( target == $u, granted == false)
then
 $c.grant();
end

rule TechnicalAdmin_targetUro
when
 $target: UsersRolesOffices()
 $c: PermissionCheck(target == $target, granted == false, action in ("Users.saveRole", "Users.removeRole"))
 UsersRolesOffices(role.name == Role.TECHNICAL_ADMIN, $target.user.hasRelationWith(this.office)) from currentOperator.usersRolesOffices
 UsersRolesOffices(role.name == Role.TECHNICAL_ADMIN, office == $target.office) from currentOperator.usersRolesOffices
then
 $c.grant();
end


rule TechnicalAdmin_InOffice
when
 $uro: UsersRolesOffices()
 $o: Office( usersRolesOffices contains $uro)
 Role(name == Role.TECHNICAL_ADMIN) from $uro.role
 $c: PermissionCheck(action in (

 	   "Institutes.edit",
 	   "Institutes.save",
	   "Institutes.delete",
	   "Offices.edit",
	   "Offices.save",
	   "Administrators.blank",
	   "Administrators.delete",
	   "Administrators.save",

	   "Users.list",
	   "Users.systemList",
	   "Users.blank",
  	   "Users.edit",
  	   "Users.updateInfo",
  	   "Users.updateRole",
  	   "Users.index",
  	   "Users.systemBlank",
  	   "Users.save",
  	   "Users.disable",
  	   "Users.disabledList",
	   "Users.enable",
	   "Users.saveRole",

	   "BadgeReaders.save",
	   "BadgeReaders.edit",
	   "BadgeReaders.updateInfo",
	   "BadgeReaders.changePassword",
	   "BadgeReaders.joinBadgeSystems",
	   "BadgeReaders.saveBadgeSystems",
	   "BadgeReaders.delete",

	   "BadgeSystems.save",
	   "BadgeSystems.edit",
	   "BadgeSystems.updateInfo",
	   "BadgeSystems.joinBadges",
	   "BadgeSystems.saveBadges",
	   "BadgeSystems.deleteBadge",
	   "BadgeSystems.joinPersonNumbers",
  	   "BadgeSystems.joinOldBadgeNumbers",
  	   "BadgeSystems.delete"

       ), target == $o, granted == false)
then
 $c.grant();
end


/*******************************************************************************
 * Azioni utente con ruolo almeno PERSONNEL_ADMIN_MINI
 ******************************************************************************/

rule AtLeastPersonnelAdminMini_Generic
when
 $uro: UsersRolesOffices()
 Role(name in (Role.PERSONNEL_ADMIN,Role.PERSONNEL_ADMIN_MINI)) from $uro.role
 $c: PermissionCheck(action in (

        /* Vecchio permesso viewPerson */
        "Persons.list",
        "MonthRecaps.showRecaps",
        "MonthRecaps.customRecap",

        /* Vecchio permesso viewPersonDay */
 		"MonthRecaps.show",

 		"Stampings.missingStamping",
        "Stampings.dailyPresence",
        "Stampings.mealTicketSituation",

        /* Vecchio permesso viewCompetence */
        "Competences.exportCompetences",
        "Competences.getOvertimeInYear",
        "Competences.approvedCompetenceInYear",

        /* Vecchio permesso viewOffice
         *  In questo caso le action non sono inserite all'interno della regola
         *  InOffice poichè i filtri sono all'interno delle stesse action.
         *  TODO rifattorizzare tutte le action di Institutes e Offices
         */
        "Offices.showOffices",
        "Institutes.list",

        /* Vecchio permesso viewCompetenceCode */
        "Competences.manageCompetenceCode",

        /*********** duplicate prima di avere il contesto office  *************/

        /* Vecchio permesso viewPerson */
        "Persons.showCurrentVacation",
        "Persons.showCurrentContractWorkingTimeType",

        "PersonDayHistory.personDayHistory",

        "Stampings.holidaySituation",
        "Stampings.personHolidaySituation",

        /* Vecchio permesso viewPersonDay */
        "Absences.manageAttachmentsPerPerson",
        "Absences.downloadAttachment",
        "Absences.zipAttachment",
        "Absences.absenceInPeriod",
		"Absences.showPersonMonthlyAbsences",
		"Absences.showGeneralMonthlyAbsences",
		"Absences.manageAttachmentsPerCode",
        "Absences.yearlyAbsences",

        "BadgeSystems.personBadges",

		"Charts.indexCharts",
        "Charts.checkLastYearAbsences",
        "Charts.whichAbsenceInYear",
     	"Charts.overtimeOnPositiveResidual",
     	"Charts.overtimeOnPositiveResidualInYear",
     	"Charts.exportHourAndOvertime",
     	"Charts.export",
     	"Charts.processLastYearAbsences",
     	"Charts.exportDataSituation",
     	"Charts.exportFinalSituation",

        "Configurations.show",
        "Configurations.edit",

     	"MealTickets.recapMealTickets",
     	"MealTickets.personMealTickets",
     	"MealTickets.recapPersonMealTickets",
        "MealTickets.editPersonMealTickets",
     	"MealTickets.returnedMealTickets",
        "MealTickets.findCodeBlock",

		"PrintTags.listPersonForPrintTags",
        "PrintTags.showTag",

        "Stampings.personStamping",

		"VacationsAdmin.list",
        "VacationsAdmin.vacationsLastYear",
        "VacationsAdmin.vacationsCurrentYear",
        "VacationsAdmin.permissionCurrentYear",

        /* Vecchio permesso viewCompetence */
        "Competences.showCompetences",
        "Competences.overtime",
        "Competences.totalOvertimeHours",
        "Competences.enabledCompetences",
        "Competences.approvedCompetenceInYear",

        /* Vecchio permesso viewWorkingTimeType */
        "WorkingTimes.manageWorkingTime",
        "WorkingTimes.manageOfficeWorkingTime",
        "WorkingTimes.showWorkingTimeType",
        "WorkingTimes.showContractWorkingTimeType",
        "WorkingTimes.showContract",
        "WorkingTimes.showHorizontal"

        ), target == null, granted == false)
then
 $c.grant();
end

rule AtLeastPersonnelAdminMini_InOffice
when
 $uro: UsersRolesOffices()
 $o: Office( usersRolesOffices contains $uro)
 Role(name in (Role.PERSONNEL_ADMIN,Role.PERSONNEL_ADMIN_MINI)) from $uro.role
 $c: PermissionCheck(action in (

 	   /* Vecchio permesso viewPerson */
 	   "Persons.list",
 	   "Persons.showCurrentVacation",
       "Persons.showCurrentContractWorkingTimeType",

       "MealTickets.recapMealTickets",
       "MealTickets.personMealTickets",
       "MealTickets.recapPersonMealTickets",
       "MealTickets.editPersonMealTickets",
       "MealTickets.returnedMealTickets",
       "MealTickets.findCodeBlock",

       "Stampings.holidaySituation",
       "Stampings.personHolidaySituation",

       /* Vecchio permesso viewPersonDay */
   	    "Absences.absenceInPeriod",
	    "Absences.showPersonMonthlyAbsences",
	    "Absences.showGeneralMonthlyAbsences",
        "Absences.yearlyAbsences",
        "Absences.manageAttachmentsPerPerson",
        "Absences.manageAttachmentsPerCode",
        "Absences.downloadAttachment",
        "Absences.zipAttachment",

		"BadgeSystems.personBadges",

		"Charts.indexCharts",
        "Charts.checkLastYearAbsences",
        "Charts.whichAbsenceInYear",
     	"Charts.overtimeOnPositiveResidual",
     	"Charts.overtimeOnPositiveResidualInYear",
     	"Charts.exportHourAndOvertime",
     	"Charts.export",
     	"Charts.processLastYearAbsences",
     	"Charts.exportDataSituation",
     	"Charts.exportFinalSituation",

     	"Configurations.show",
        "Configurations.edit",

        "MonthRecaps.showRecaps",
        "MonthRecaps.customRecap",
        "PrintTags.listPersonForPrintTags",
        "PrintTags.showTag",

        "Stampings.personStamping",
        "Stampings.dailyPresence",
        "Stampings.missingStamping",

        "VacationsAdmin.list",
        "VacationsAdmin.vacationsLastYear",
        "VacationsAdmin.vacationsCurrentYear",
        "VacationsAdmin.permissionCurrentYear",

        /* Vecchio permesso viewCompetence */
        "Competences.showCompetences",
        "Competences.overtime",
        "Competences.totalOvertimeHours",
        "Competences.enabledCompetences",
        "Competences.approvedCompetenceInYear",

        /* Vecchio permesso viewWorkingTimeType */
        "WorkingTimes.manageWorkingTime",
        "WorkingTimes.manageOfficeWorkingTime",
        "WorkingTimes.showWorkingTimeType",
        "WorkingTimes.showContractWorkingTimeType",
        "WorkingTimes.showContract",
        "WorkingTimes.showHorizontal",
        "WorkingTimes.insertVerticalWorkingTime"

       ), target == $o, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Azioni utente con ruolo solo PERSONNEL_ADMIN
 ******************************************************************************/

rule OnlyForPersonnelAdmin_Generic
when
 $uro: UsersRolesOffices()
 Role(name == Role.PERSONNEL_ADMIN) from $uro.role
 $c: PermissionCheck(action in (

 		/*Metodi per la gestione delle assenze bypassando i controlli*/
 		"Absences.forceAbsences",
 		"Absences.forceRemoval",
 		"Absences.forceInsert",

 		/* Vecchio permesso editPerson */
        "Persons.insertPerson",

		/* Vecchio permesso editAbsenceCode */
		/* FIXME: stesso ragionamento del editCompetenceCode vedi sotto */
		"Absences.manageAbsenceCode",

		/*********** duplicate prima di avere il contesto office  *************/

		/* Vecchio permesso editPerson */
        "Persons.edit",
        "Persons.update",
        "Persons.save",
     	"Persons.insertUsername",
     	"Persons.updateUsername",
        "Persons.deletePerson",
     	"Persons.deletePersonConfirmed",
     	"Configurations.personShow",
     	"Configurations.personEdit",
     	"Configurations.personUpdate",

        "Contracts.personContracts",
        "Contracts.insert",
        "Contracts.save",
        "Contracts.edit",
        "Contracts.update",
        "Contracts.delete",

        "Contracts.updateSourceContract",
     	"Contracts.saveResidualSourceContract",
     	"Contracts.saveMealTicketSourceContract",
		"Contracts.updateContractStampProfile",
        "Contracts.saveContractStampProfile",
        "Contracts.updateContractWorkingTimeType",
        "Contracts.saveContractWorkingTimeType",
        "Contracts.updateContractVacationPeriod",
        "Contracts.saveContractVacationPeriod",

     	"Persons.children",
     	"Persons.editChild",
     	"Persons.saveChild",
     	"Persons.insertChild",
     	"Persons.deleteChild",

     	"Persons.workGroup",
     	"Persons.confirmGroup",
     	"Persons.removePersonFromGroup",

     	"Persons.modifySendEmail",
     	"Persons.updateSendEmail",

     	"MealTickets.submitPersonMealTicket",
     	"MealTickets.returnPersonCodeBlock",
     	"MealTickets.performReturnPersonCodeBlock",
        "MealTickets.deletePersonCodeBlock",
        "MealTickets.performDeletePersonCodeBlock",
        "MealTickets.mealTicketsLegacy",

        /* Vecchio permesso editPersonDay */
		"Absences.blank",
     	"Absences.save",
     	"Absences.edit",
     	"Absences.delete",
     	"Absences.addAttach",
     	"Absences.removeAttach",

        "Stampings.blank",
       	"Stampings.edit",
     	"Stampings.save",
     	"Stampings.delete",
     	"Stampings.toggleWorkingHoliday",
     	"Stampings.forceMealTicket",

     	/* Vecchio permesso editCompetence */
        "Competences.saveOvertime",
        "Competences.updatePersonCompetence",
        "Competences.saveNewCompetenceConfiguration",
        "Competences.insertCompetence",
        "Competences.editCompetence",
        "Competences.saveCompetence",
        "Competences.addService",
        "Competences.editService",
        "Competences.activateServices",
        "Competences.saveService",
        "Competences.evaluateService",

        /* Vecchio permesso uploadSituation */
        "UploadSituation.uploadData",
        "UploadSituation.updateSession",
        "UploadSituation.createFile",
        "UploadSituation.computeCreateFile",
        "UploadSituation.fetchData",
        "UploadSituation.logoutAttestati",
        "UploadSituation.processAllPersons",
        "UploadSituation.processSinglePerson",
        "UploadSituation.showProblems",
        "UploadSituation.showCertificatedData",
        "UploadSituation.checkData",
        "UploadSituation.performCheckData",

        "Certifications.certifications",
        "Certifications.processAll",
        "Certifications.emptyCertifications",

         /* Vecchio permesso editWorkingTimeType */
        "WorkingTimes.insertWorkingTime",
        "WorkingTimes.save",
        "WorkingTimes.delete",
        "WorkingTimes.toggleWorkingTimeTypeEnabled",
        "WorkingTimes.changeWorkingTimeTypeToAll",
        "WorkingTimes.executeChangeWorkingTimeTypeToAll",
        "WorkingTimes.saveHorizontal",
        "WorkingTimes.insertWorkingTimeBaseInformation",
        "WorkingTimes.insertVerticalWorkingTime",

        /* Vecchio permesso editCompetenceCode */
        /* FIXME vedi in regola onlyForPersonnelAdmin_InOffice */
        "Competences.edit",
        "Competences.save",

        /* Vecchio permesso editAbsenceCode */
		/* FIXME: stesso ragionamento del editCompetenceCode vedi sotto */
        "Absences.editCode",

     	"Configurations.show",
     	"Configurations.edit",
        "Configurations.update",

		"BadgeSystems.joinBadgesPerson",
		"BadgeSystems.saveBadges",
		"BadgeSystems.deleteBadgePerson",
		"BadgeSystems.deleteBadge",
		/*ore di formazione*/
		"PersonMonths.visualizePeopleTrainingHours",
        "PersonMonths.insertPeopleTrainingHours",
        "PersonMonths.modifyPeopleTrainingHours",
        "PersonMonths.deletePeopleTrainingHours",
        "PersonMonths.deletePeopleTrainingHoursConfirmed",
        "PersonMonths.modifyTrainingHours",
        "PersonMonths.save",
        "PersonMonths.saveTrainingHours",
        "Charts.listForExcelFile",
        "Charts.test"

        ), target == null, granted == false)
then
 $c.grant();
end

rule OnlyForPersonnelAdmin_InOffice
when
 $uro: UsersRolesOffices()
 $o: Office( usersRolesOffices contains $uro)
 Role(name == Role.PERSONNEL_ADMIN) from $uro.role
 $c: PermissionCheck(action in (

 		/* Vecchio permesso editPerson */
        "Persons.edit",
        "Persons.update",
        "Persons.save",
     	"Persons.insertUsername",
     	"Persons.updateUsername",
        "Persons.deletePerson",
     	"Persons.deletePersonConfirmed",

     	"Configurations.personShow",
     	"Configurations.personEdit",
     	"Configurations.personUpdate",

        "Contracts.personContracts",
        "Contracts.insert",
        "Contracts.save",
        "Contracts.edit",
        "Contracts.update",
        "Contracts.delete",

        "Contracts.updateSourceContract",
     	"Contracts.saveResidualSourceContract",
     	"Contracts.saveMealTicketSourceContract",
		"Contracts.updateContractStampProfile",
        "Contracts.saveContractStampProfile",
        "Contracts.updateContractWorkingTimeType",
        "Contracts.saveContractWorkingTimeType",
        "Contracts.updateContractVacationPeriod",
        "Contracts.saveContractVacationPeriod",

     	"Persons.children",
     	"Persons.editChild",
     	"Persons.saveChild",
     	"Persons.insertChild",
     	"Persons.deleteChild",

     	"Persons.workGroup",
     	"Persons.confirmGroup",
     	"Persons.removePersonFromGroup",

     	"Persons.modifySendEmail",
     	"Persons.updateSendEmail",

     	"MealTickets.submitPersonMealTicket",
     	"MealTickets.returnPersonCodeBlock",
     	"MealTickets.performReturnPersonCodeBlock",
        "MealTickets.deletePersonCodeBlock",
        "MealTickets.performDeletePersonCodeBlock",
        "MealTickets.mealTicketsLegacy",

        /* Vecchio permesso editPersonDay */
 		"Absences.blank",
     	"Absences.save",
     	"Absences.edit",
     	"Absences.delete",
     	"Absences.addAttach",
     	"Absences.removeAttach",

       	"Stampings.blank",
       	"Stampings.edit",
     	"Stampings.save",
     	"Stampings.delete",
     	"Stampings.toggleWorkingHoliday",
     	"Stampings.forceMealTicket",

     	/* Vecchio permesso editCompetence */
        "Competences.saveOvertime",
        "Competences.updatePersonCompetence",
        "Competences.saveNewCompetenceConfiguration",
        "Competences.insertCompetence",
        "Competences.editCompetence",
        "Competences.saveCompetence",
        "Competences.addService",
        "Competences.editService",
        "Competences.activateServices",
        "Competences.saveService",
        "Competences.evaluateService",

         /* Vecchio permesso uploadSituation */
        "UploadSituation.uploadData",
        "UploadSituation.updateSession",
        "UploadSituation.createFile",
        "UploadSituation.computeCreateFile",
        "UploadSituation.fetchData",
        "UploadSituation.logoutAttestati",
        "UploadSituation.processAllPersons",
        "UploadSituation.processSinglePerson",
        "UploadSituation.showProblems",
        "UploadSituation.showCertificatedData",
        "UploadSituation.checkData",
        "UploadSituation.performCheckData",

        "Certifications.certifications",
        "Certifications.processAll",
        "Certifications.emptyCertifications",

        /* Vecchio permesso editWorkingTypeType */
        "WorkingTimes.insertWorkingTime",
        "WorkingTimes.save",
        "WorkingTimes.delete",
        "WorkingTimes.toggleWorkingTimeTypeEnabled",
        "WorkingTimes.insertWorkingTime",
        "WorkingTimes.changeWorkingTimeTypeToAll",
        "WorkingTimes.executeChangeWorkingTimeTypeToAll",
        "WorkingTimes.saveHorizontal",
        "WorkingTimes.insertWorkingTimeBaseInformation",
        "WorkingTimes.insertVerticalWorkingTime",

        /* Vecchio permesso editCompetenceCode
         *  FIXME: i codici competenza non sono associati all'office. Quindi non ha senso
 	     *  impostare la regola editCompetenceCode_InOffice. O si associano all'office in modo
 	     *  che ogni ufficio possa avere i propri privati (come in workingTimeType) oppure si
 	     *  rimuove questo permesso ai PersonellAdmin e si delega esclusivamente a utente SuperUser.
 	     *  Quest'ultima soluzione potrebbe essere la più opportuna e meno dispendiosa in ottica
 	     *  centralizzazione.
 	     */
        "Competences.edit",
        "Competences.save",

        /* Vecchio permesso editAbsenceCode */
		/* FIXME: stesso ragionamento del editCompetenceCode*/
        "Absences.editCode",

     	"Configurations.show",

		"BadgeSystems.joinBadgesPerson",
		"BadgeSystems.saveBadges",
		"BadgeSystems.deleteBadgePerson",
		"BadgeSystems.deleteBadge",

		/*Ore di formazione*/
		"PersonMonths.visualizePeopleTrainingHours",
        "PersonMonths.insertPeopleTrainingHours",
        "PersonMonths.deletePeopleTrainingHours",
        "PersonMonths.deletePeopleTrainingHoursConfirmed",
        "PersonMonths.modifyTrainingHours",
        "PersonMonths.modifyPeopleTrainingHours",
        "PersonMonths.save",
        "PersonMonths.saveTrainingHours",
        "Charts.listForExcelFile",
        "Charts.test"

       ), target == $o, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Una configurazione può essere modificata se....
 ******************************************************************************/
rule canEditConfiguration
when
  $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN)
  $target: Configuration(office == $uro.office, epasParam != EpasParam.TR_AUTOCERTIFICATION)
  $c: PermissionCheck(action in ("Configurations.edit", "Configurations.update") , target == $target, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * La Configurazione per abilitare l'autocertificazione può essere abilitata se...
 ******************************************************************************/
rule canEditAutocertificationConf
when
  $uro: UsersRolesOffices(role.name == Role.PERSONNEL_ADMIN)
  $target: Configuration(office == $uro.office, epasParam == EpasParam.TR_AUTOCERTIFICATION)
  $o: Office() from $uro.office
  Attachment(type == AttachmentType.TR_AUTOCERTIFICATION) from $o.attachments
  $c: PermissionCheck(action in ("Configurations.edit", "Configurations.update") , target == $target, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Azioni per utente employee abilitato alla timbratura fuori sede
 ******************************************************************************/
rule Working_offsite
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person() from currentOperator.person
 $o: Office() from $p.office
 Configuration(epasParam == EpasParam.WORKING_OFF_SITE, fieldValue == true) from $o.configurations
 PersonConfiguration(epasParam == EpasParam.OFF_SITE_STAMPING, fieldValue == true) from $p.personConfigurations
 $c: PermissionCheck(action in (
    "Stampings.blank",
    "Stampings.edit",
    "Stampings.save",
    "Stampings.delete",
    "Absences.blank",
    "Absences.edit",
    "Absences.delete",
    "Absences.save"
 ), granted == false, target == null)
then
 $c.grant();
end


/*******************************************************************************
 * Un dipendente può autocertificare le proprie timbrature per il lavoro fuori Sede se...
 ******************************************************************************/
rule employeeCanInsertStampingsOffsite
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person() from currentOperator.person
 $o: Office() from $p.office
 Configuration(epasParam == EpasParam.WORKING_OFF_SITE, fieldValue == true) from $o.configurations
 PersonConfiguration(epasParam == EpasParam.OFF_SITE_STAMPING, fieldValue == true) from $p.personConfigurations
 $c: PermissionCheck(action in ("Stampings.blank"), target == $p, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Un dipendente può modificare le sue timbrature per il lavoro fuori Sede se...
 ******************************************************************************/
rule employeeCanEditStampingsOffsite
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person() from currentOperator.person
 $o: Office() from $p.office
 $target: Stamping(UserDao.getAllowedStampTypes(currentOperator).contains(stampType), this.getOwner() == $p)
 Configuration(epasParam == EpasParam.WORKING_OFF_SITE, fieldValue == true) from $o.configurations
 PersonConfiguration(epasParam == EpasParam.OFF_SITE_STAMPING, fieldValue == true) from $p.personConfigurations
 $c: PermissionCheck(action in ("Stampings.edit", "Stampings.save", "Stampings.delete"), target == $target, granted == false)
 eval (!$target.isPersistent() || HistoricalDao.lastRevisionOperator($target) == currentOperator)
 /* AGGIUNGERE IL CONTROLLO SULL'INVIO DEGLI ATTESTATI!!! */
then
 $c.grant();
end

/*
 * L'impiegato può modificare/salvare una determinata assenza se:
 */
rule EmployeeCanEditAbsence
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $a: Absence()
 $at: AbsenceType(CodesForEmployee.getCodes().contains(code)) from $a.absenceType
 $pd: PersonDay() from $a.personDay
 $p: Person(this == currentOperator.person) from $pd.person
 $o: Office() from $p.office
 Configuration(epasParam == EpasParam.WORKING_OFF_SITE, fieldValue == true) from $o.configurations
 PersonConfiguration(epasParam == EpasParam.OFF_SITE_STAMPING, fieldValue == true) from $p.personConfigurations
 $c: PermissionCheck(target == $a, granted == false )
then
 $c.grant();
end

/*******************************************************************************
 * Un dipendente può autocertificare le proprie timbrature se...
 ******************************************************************************/
rule employeeAutocertificationStampings
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person(qualification.qualification <= 3) from currentOperator.person
 $o: Office() from $p.office
 Configuration(epasParam == EpasParam.TR_AUTOCERTIFICATION, fieldValue == true) from $o.configurations
 $c: PermissionCheck(action in ("Stampings.blank", "Stampings.edit", "Stampings.save" , "Stampings.delete" ), target == null, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Un dipendente può autocertificare le proprie timbrature se...
 ******************************************************************************/
rule employeeCanInsertStampings
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person(qualification.qualification <= 3) from currentOperator.person
 $o: Office() from $p.office
 Configuration(epasParam == EpasParam.TR_AUTOCERTIFICATION, fieldValue == true) from $o.configurations
 $c: PermissionCheck(action in ("Stampings.blank"), target == $p, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Un dipendente può modificare le sue timbrature se...
 ******************************************************************************/
rule employeeCanEditStampings
when
 $uro: UsersRolesOffices(role.name == Role.EMPLOYEE)
 $p: Person(qualification.qualification <= 3) from currentOperator.person
 $o: Office() from $p.office
 $target: Stamping(this.getOwner() == $p)
 Configuration(epasParam == EpasParam.TR_AUTOCERTIFICATION, fieldValue == true) from $o.configurations
 $c: PermissionCheck(action in ("Stampings.edit", "Stampings.save" , "Stampings.delete"), granted == false, target == $target)
 eval (!$target.isPersistent() || HistoricalDao.lastRevisionOperator($target) == currentOperator)
  /* AGGIUNGERE IL CONTROLLO SULL'INVIO DEGLI ATTESTATI!!! */
then
 $c.grant();
end

/*******************************************************************************
 * Azioni utente con ruolo BADGE_READER
 ******************************************************************************/

rule Stampings_create
when
 $uro: UsersRolesOffices()
 Role(name == Role.BADGE_READER) from $uro.role
 $c: PermissionCheck(action in (

 		/* duplicate */
 		"StampingsFromClient.create",
 		"StampingsFromClient.createNotRecompute",
 		"StampingsFromClient.absence",
 		"StampingsFromClient.absenceNotRecompute"

 		), target == null, granted == false)
 then
 $c.grant();
end

rule Stampings_create_InOffice
when
 $uro: UsersRolesOffices()
 $o: Office( usersRolesOffices contains $uro)
 Role(name == Role.BADGE_READER) from $uro.role
 $c: PermissionCheck(action in (

 		"StampingsFromClient.create",
 		"StampingsFromClient.createNotRecompute",
 		"StampingsFromClient.absence",
 		"StampingsFromClient.absenceNotRecompute"

 		), target == $o, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Azioni utente con ruolo REST_CLIENT
 ******************************************************************************/
rule Rest
when
 $uro: UsersRolesOffices()
 $p: Role(name == "restClient") from $uro.role
 $c: PermissionCheck(action in (

        "rest.Persons.days",
        "rest.Persons.missions",
        "rest.Persons.competences",
        "rest.Absences.absencesInPeriod",
        "rest.Absences.insertAbsence",
        "rest.Absences.checkAbsence",

        "rest.Absences.checkAbsenceByPerseoId",
        "rest.Absences.insertAbsenceByPerseoId"

       ), target == null, granted == false)
then
 $c.grant();
end

rule Rest_inOffice
when
 $uro: UsersRolesOffices()
 $o: Office( usersRolesOffices contains $uro)
 $p: Role(name == "restClient") from $uro.role
 $c: PermissionCheck(action in (

        "rest.Absences.absencesInPeriod",
        "rest.Absences.insertAbsence",
        "rest.Absences.checkAbsence",

        "rest.Absences.checkAbsenceByPerseoId",
        "rest.Absences.insertAbsenceByPerseoId"

       ), target == $o, granted == false)
then
 $c.grant();
end

/*******************************************************************************
 * Azioni via REST e iCal per Turni e Reperibilità
 ******************************************************************************/

/**
rule viewShiftICal
when
  $uro: UsersRolesOffices()
  $st: ShiftType(shiftCategories.supervisor == $uro.user || )
  $c: PermissionCheck(action in (
        "Shift.iCal"
       ), target == null, granted == false)
then
 $c.grant();
end
*/

rule ManageShift
when
 $uro: UsersRolesOffices()
 $p: Role(name == "shiftManager") from $uro.role
 $c: PermissionCheck(action in (

        "Shift.personList",
        "Shift.timeTable",
        "Shift.find",
        "Shift.update",
        "Shift.exportMonthAsPDF",
        "Shift.exportMonthCalAsPDF",
        "Shift.absence",
        "Shift.iCal"
       ), target == null, granted == false)
then
 $c.grant();
end

rule ManageReperibility
when
 $uro: UsersRolesOffices()
 $p: Role(name == "reperibilityManager") from $uro.role
 $c: PermissionCheck(action in (

        "Reperibility.personList",
        "Reperibility.find",
        "Reperibility.who",
        "Reperibility.absence",
        "Reperibility.whoIsAbsent",
        "Reperibility.update",
        "Reperibility.changePeriods",
        "Reperibility.exportYearAsPDF",
        "Reperibility.exportMonthAsPDF",
        "Reperibility.iCal"

       ), target == null, granted == false)
then
 $c.grant();
end

/*
 * Permessi sulle proprie notifiche
 */
rule Notifications
when
  $n: Notification(recipient == currentOperator)
  $c: PermissionCheck(target == $n, granted == false)
then
  $c.grant();
end
